<?xml version="1.0" encoding="ASCII"?>


<!--
  The MIT License (MIT)
  
  Copyright (c) 2010 Technische Universitaet Berlin
  
  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:
  
  The above copyright notice and this permission notice shall be included in
  all copies or substantial portions of the Software.
  
  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
  THE SOFTWARE.
-->

<!DOCTYPE stylesheet
  [
   <!ENTITY br  "&#xA;">
   <!ENTITY sp  "  ">
  ]
>

<!--
   Author:  Tilman Rassy

   $Id: xsl_summary.content.xml,v 1.3 2009/08/17 14:25:17 rassy Exp $
-->

<xsl:stylesheet version="1.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:xsl-ext="http://www.mumie.net/xml-namespace/xsl-ext"
                xmlns:mumie="http://www.mumie.net/xml-namespace/document/metainfo"
                xmlns:sum="http://www.mumie.net/xml-namespace/document/summary"
                xmlns:mtx="http://www.mumie.net/xml-namespace/mmtex"
                xmlns:math="http://www.w3.org/1998/Math/MathML"
                xmlns:math-ext="http://www.mumie.net/xml-namespace/mathml-ext"
                xmlns="http://www.w3.org/1999/xhtml"
                exclude-result-prefixes="xsl-ext mumie sum mtx math math-ext">

<xsl-ext:import lid="xsl-util" internal="yes"/>
<xsl-ext:import lid="xsl-mmtex-stdlayout" internal="yes"/>
<xsl-ext:import lid="xsl-mmtex-struct" internal="yes"/>
<xsl-ext:import lid="xsl-mmtex-math" internal="yes"/>
<xsl-ext:import lid="xsl-mmtex-media" internal="yes"/>
<xsl-ext:import lid="xsl-summary-i18n" internal="yes"/>

<xsl:output method="xml"
            doctype-public="-//W3C//DTD XHTML 1.1 plus MathML 2.0//EN"
            doctype-system="http://www.w3.org/TR/MathML2/dtd/xhtml-math11-f.dtd"
            encoding="ASCII"/>

<xsl-ext:insert-japs-xsl-lib/>

<!--
  ================================================================================
  Global variables
  ================================================================================
-->

<!-- HTML id of the main element, i.e. the "table" element containing all the content -->
<xsl:variable name="main-element-html-id">_MAIN_</xsl:variable>

<!--
  ================================================================================
  Header
  ================================================================================
-->    

<xsl:template name="header">
  <table class="header">
    <tr>
      <td>
        <div class="doctype">
          <xsl:value-of select="$i18n-summary"/>
        </div>
      </td>
      <td>
        <div class="title">
          <xsl:apply-templates select="/*/mumie:content/sum:summary/mtx:title" mode="header"/>
        </div>
      </td>
    </tr>
  </table>
</xsl:template>

<xsl:template match="mtx:title" mode="header">
  <xsl:apply-templates/>
</xsl:template>

<xsl:template match="mtx:title">
  <!-- Ignored in normal processing -->
</xsl:template>

<!--
  ================================================================================
  Footer
  ================================================================================
-->

<xsl:template name="footer">
  <xsl:variable name="doctype" select="local-name(/*)"/>
  <xsl:variable name="id" select="/*/@id"/>
  <div class="footer">
    <span class="footer">
      <!-- Copyright note: -->
      <xsl:value-of select="/*/mumie:copyright"/>
      <span class="sep">&#183;</span>
      <!-- Type and id: -->
      <xsl:value-of select="concat($doctype, ' ', $id)"/>
      <span class="sep">&#183;</span>
      <!-- Link to info page: -->
      <a>
        <xsl:attribute name="href">
        <xsl:call-template name="xsl.document-url">
          <xsl:with-param name="document-type.name" select="local-name(/*)"/>
          <xsl:with-param name="context" select="'info'"/>
          <xsl:with-param name="id" select="/*/@id"/>
        </xsl:call-template>
        </xsl:attribute>
        <xsl:attribute name="target">_top</xsl:attribute>
        <xsl:attribute name="title">
          <xsl:value-of select="$i18n-info-expl"/>
        </xsl:attribute>
        <xsl:value-of select="$i18n-info"/>
      </a>
      <span class="sep">&#183;</span>
      <!-- "Powered by MUMIE" -->
      <xsl:call-template name="powered-by-mumie"/>
    </span>
  </div>
</xsl:template>

<!--
  ================================================================================
  Elements in the "sum" namespace
  ================================================================================
-->

<xsl:template match="sum:ifpoints">
  <div class="meta">
    <div class="comment">
      ifpoints <xsl:value-of select="@min"/> .. <xsl:value-of select="@max"/>
    </div>
    <xsl:apply-templates/>
  </div>
</xsl:template>

<xsl:template match="sum:ifscore">
  <div class="meta">
    <div class="comment">
      ifscore <xsl:value-of select="@min"/> .. <xsl:value-of select="@max"/>
    </div>
    <xsl:apply-templates/>
  </div>
</xsl:template>

<xsl:template match="sum:iftimeframe">
  <div class="meta">
    <div class="comment">
      iftimeframe <xsl:value-of select="@relation"/>
    </div>
    <xsl:apply-templates/>
  </div>
</xsl:template>

<xsl:template match="sum:ifstate">
  <div class="meta">
    <div class="comment">
      ifstate <xsl:value-of select="@state"/>
    </div>
    <xsl:apply-templates/>
  </div>
</xsl:template>

<!--
  ================================================================================
  Top-level structure
  ================================================================================
-->    

<xsl:template match="/">
  <html>
    <head>
      <title>
        <xsl:value-of select="/*/mumie:name"/>
      </title>
      <link rel="stylesheet" type="text/css">
        <xsl:attribute name="href"><xsl-ext:url lid="css-base"/></xsl:attribute>
      </link>
      <link rel="stylesheet" type="text/css">
        <xsl:attribute name="href"><xsl-ext:url lid="css-mmtex-stdlayout"/></xsl:attribute>
      </link>
      <link rel="stylesheet" type="text/css">
        <xsl:attribute name="href"><xsl-ext:url lid="css-element"/></xsl:attribute>
      </link>
      <link rel="stylesheet" type="text/css">
        <xsl:attribute name="href"><xsl-ext:url lid="css-summary"/></xsl:attribute>
      </link>
      <style type="text/css">
        div.meta
        {
          margin: 2px 2px 2px 2px;
          padding: 2px 2px 2px 2px;
          border-style: dotted;
          border-width: 1px;
          border-color: #235642;
        }
        div.meta div.comment
        {
          margin: 0em 0em 0em 0em;
          padding: 1px 0em 0em 2px;
          color: #235642;
          font-size: 80%;
        }
      </style>
    </head>
    <body>
      <table id="{$main-element-html-id}" class="root">
        <tr>
          <td>
            <!-- Header: -->
            <xsl:call-template name="header"/>
          </td>
        </tr>
        <tr>
          <td>
            <!-- Main: -->
            <div class="plain">
              <xsl:apply-templates select="/*/mumie:content/sum:summary/*"/>
            </div>
          </td>
        </tr>
        <tr>
          <td colspan="2">
            <!-- Footer: -->
            <xsl:call-template name="footer"/>
          </td>
        </tr>
      </table>
    </body>
  </html>
</xsl:template>

</xsl:stylesheet>