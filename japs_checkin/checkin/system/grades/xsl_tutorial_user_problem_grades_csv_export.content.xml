<?xml version="1.0" encoding="ASCII"?>


<!--
  The MIT License (MIT)
  
  Copyright (c) 2010 Technische Universitaet Berlin
  
  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:
  
  The above copyright notice and this permission notice shall be included in
  all copies or substantial portions of the Software.
  
  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
  THE SOFTWARE.
-->

<xsl:stylesheet version="1.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:xsl-ext="http://www.mumie.net/xml-namespace/xsl-ext"
                xmlns:grades="http://www.mumie.net/xml-namespace/grades"
                xmlns:mumie="http://www.mumie.net/xml-namespace/document/metainfo"
                xmlns="http://www.w3.org/1999/xhtml"
                exclude-result-prefixes="xsl-ext grades">
  

<xsl-ext:import lid="xsl-problem-grades-i18n" internal="yes"/>
<xsl-ext:import lid="xsl-problem-grades-grouping-i18n" internal="yes"/>

<xsl:output method="text"
            doctype-public="-//W3C//DTD XHTML 1.1 plus MathML 2.0//EN"
            doctype-system="http://www.w3.org/TR/MathML2/dtd/xhtml-math11-f.dtd"
            encoding="ISO-8859-1"/>


<!-- ================================================================================ -->
<!-- h1: Keys                                                                         -->
<!-- ================================================================================ -->

<xsl:key name="user-problem-grade"
         match="/*/grades:user_problem_grade"
         use="concat(@user_id, ',', @ref_id)"/>

<xsl:key name="problem"
         match="/*/grades:problem"
         use="concat(@target, ',', @worksheet_id)"/>

<xsl:key name="problem-target"
         match="/*/grades:problem"
         use="@target"/>

<xsl:key name="worksheets"
         match="/*/grades:worksheet"
         use="@target"/>


<xsl:key name="user-problem-grade-target"
         match="/*/grades:user_problem_grade"
         use="concat(@user_id, ',', @target)"/>



<xsl:template name="grades">
  
  
  <xsl:for-each select="/*/grades:user">
    <xsl:sort select="@surname"/>
    <xsl:sort select="@first_name"/>
    <xsl:variable name="user-id" select="@id"/>
    <xsl:value-of select="@surname"/><xsl:text>;</xsl:text>   
    <xsl:value-of select="@first_name"/><xsl:text>;</xsl:text>   
    <xsl:value-of select="@id"/><xsl:text>;</xsl:text> 
    
    <xsl:for-each select="/*/grades:target">
      <xsl:variable name="target" select="@name"/>
      <xsl:variable name="worksheets" select="key('worksheets',current()/@name)"/>
      <xsl:variable name="problems" select="key('problem-target', current()/@name)"/>
      
      <xsl:choose>
        <xsl:when test="count($worksheets) != 0">
          
          <xsl:for-each select="$worksheets">
            <xsl:sort select="@label"/>
            <xsl:variable name="worksheet-id" select="@id"/>
            <xsl:variable name="ref-id" select="key('problem', concat($target, ',',  $worksheet-id))/@ref_id"/>
            
            <xsl:value-of select="round(sum(
                          key('user-problem-grade-target', concat($user-id, ',', $target))
                          [@ref_id=$ref-id and @status='correction']/@result)
                          * 10) div 10"/>
            <xsl:text>;</xsl:text>
          </xsl:for-each>
        </xsl:when>
        <xsl:when test="count($problems) != 0 and @name = '06'"><!-- check if category is traditional  -->
          <xsl:for-each select="$problems">
            <xsl:sort select="@label"/>     
            <xsl:variable name="ref-id" select="@ref_id"/>
            <xsl:value-of select="round(sum(
                                key('user-problem-grade-target', concat($user-id, ',', $target))
                                [@ref_id=$ref-id and @status='correction']/@result)
                      * 10) div 10"/>
            <xsl:text>;</xsl:text>
          </xsl:for-each>
        </xsl:when>
      </xsl:choose>
      
    </xsl:for-each> <!--target-->
    
    
    
  <xsl:text>&#xa;</xsl:text> 
  </xsl:for-each> <!--user-->

</xsl:template>

<xsl:template name="target_names">
  <xsl:param name="target"/>
    <xsl:choose>
      <xsl:when test="$target= '01'">
        <xsl:value-of select="$i18n-target01"/>
      </xsl:when>
      <xsl:when test="$target= '02'">
        <xsl:value-of select="$i18n-target02"/>
      </xsl:when>
      <xsl:when test="$target= '03'">
        <xsl:value-of select="$i18n-target03"/>
      </xsl:when>
      <xsl:when test="$target= '04'">
        <xsl:value-of select="$i18n-target04"/>
      </xsl:when>      
      <xsl:when test="$target= '05'">
        <xsl:value-of select="$i18n-target05"/>
      </xsl:when>      
      <xsl:when test="$target= '06'">
        <xsl:value-of select="$i18n-target06"/>
      </xsl:when>
    </xsl:choose>
</xsl:template>

<xsl:template name="worksheet_labels">
  
  <xsl:for-each select="/*/grades:target">
    <xsl:variable name="worksheets" select="key('worksheets', current()/@name)"/>
    <xsl:variable name="problems" select="key('problem-target', current()/@name)"/>
    
    <xsl:choose>
      <xsl:when test="count($worksheets) != 0">
        <xsl:for-each select="$worksheets">
          <xsl:sort select="@label"/>
          <xsl:call-template name="target_names">
            <xsl:with-param name="target" select="@target"/>
          </xsl:call-template>
          <xsl:text> : </xsl:text>
          <xsl:value-of select="@label"/>
          <xsl:text>;</xsl:text>        
        </xsl:for-each>
      </xsl:when>
      <xsl:when test="count($problems) != 0 and @name = '06'"><!-- check if categorz is traditional  -->
        <xsl:for-each select="$problems">
          <xsl:sort select="@label"/>
          <xsl:call-template name="target_names">
            <xsl:with-param name="target" select="@target"/>
          </xsl:call-template>
          <xsl:text> : </xsl:text>
          <xsl:value-of select="/*/grades:worksheet[@id=current()/@worksheet_id]/@label"/>
          <xsl:text>;</xsl:text>        
        </xsl:for-each>
      </xsl:when>
    </xsl:choose>
    
  </xsl:for-each>
</xsl:template>
    <!-- ================================================================================ -->
    <!-- h1: Creating the page                                                            -->
    <!-- ================================================================================ -->
    
    <xsl:template match="/">

      <xsl:value-of select="$i18n-surname"/><xsl:text>;</xsl:text>
      <xsl:value-of select="$i18n-firstname"/><xsl:text>;</xsl:text>  
      <xsl:value-of select="$i18n-id"/><xsl:text>;</xsl:text>    
      <xsl:call-template name="worksheet_labels"/>
      <xsl:text>&#xa;</xsl:text>  
      <xsl:call-template name="grades"/>  
    </xsl:template>
        
</xsl:stylesheet>
