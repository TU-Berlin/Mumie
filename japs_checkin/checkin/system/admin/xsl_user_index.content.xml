<?xml version="1.0" encoding="ASCII"?>


<!--
  The MIT License (MIT)
  
  Copyright (c) 2010 Technische Universitaet Berlin
  
  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:
  
  The above copyright notice and this permission notice shall be included in
  all copies or substantial portions of the Software.
  
  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
  THE SOFTWARE.
-->

<!DOCTYPE stylesheet
  [
   <!ENTITY br  "&#xA;">
   <!ENTITY sp  "  ">
  ]
>

<!--
   Author:  Marek Grudzinski <grudzin@math.tu-berlin.de>
   $Id: xsl_user_index.content.xml,v 1.3 2009/12/14 11:18:32 linges Exp $
-->

<xsl:stylesheet version="1.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:xsl-ext="http://www.mumie.net/xml-namespace/xsl-ext"
                xmlns:mumie="http://www.mumie.net/xml-namespace/document/metainfo"
                xmlns="http://www.w3.org/1999/xhtml"
                exclude-result-prefixes="xsl-ext mumie">

<xsl-ext:import lid="xsl-util" internal="yes"/>
<xsl-ext:import lid="xsl-i18n" internal="yes"/>

<xsl:output method="xml"
            doctype-public="-//W3C//DTD XHTML 1.1 plus MathML 2.0//EN"
            doctype-system="http://www.w3.org/TR/MathML2/dtd/xhtml-math11-f.dtd"
            encoding="ASCII"/>

<xsl-ext:insert-japs-xsl-lib/>

<!--
  Main template
-->

<xsl:template match="/">
  <html>
    <head>
      <title><xsl:value-of select="$i18n-title"/></title>
      <link rel="stylesheet" type="text/css">
        <xsl:attribute name="href"><xsl-ext:url lid="css-base"/></xsl:attribute>
      </link>
    </head>
    <body>
      <xsl:call-template name="top-bar"/>
      <div class="main-bounded-width">
        <!-- Headline: -->
        <div class="h1-container">
          <div class="breadcrumb">
            <a xsl-ext:href="pge-admin-home"><xsl:value-of select="$i18n-admin"/></a> &gt;
            <a xsl-ext:href="pge-user-management"><xsl:value-of select="$i18n-user-management"/></a> &gt;
          </div>
          <h1><xsl:value-of select="$i18n-user-index"/></h1>
        </div>
        

        <!-- Table of section contents: -->
        <table class="genuine">
          <thead>
            <tr>
              <td><xsl:value-of select="$i18n-name"/></td>
              <td><xsl:value-of select="$i18n-id"/></td>
              <td><xsl:value-of select="$i18n-login"/></td>
              <td><xsl:value-of select="$i18n-email"/></td>
              <td><xsl:value-of select="$i18n-groups"/></td>
              <td></td>
            </tr>
          </thead>
          <tbody>
            <xsl:for-each select="/mumie:pseudo_documents/mumie:user">
              <xsl:sort select="mumie:surname"/>
              <tr>
                <td>
                  <xsl:value-of select="mumie:surname"/>,<xsl:value-of select="mumie:first_name"/>
                </td>
                <td class="number">
                  <xsl:value-of select="./@id"/>
                </td>
                <td>
                  <xsl:value-of select="mumie:login_name"/>
                </td>
                <td>
                  <xsl:if test="mumie:email != ''">
                    <xsl:call-template name="email">
                      <xsl:with-param name="email" select="mumie:email"/>
                    </xsl:call-template>
                  </xsl:if>
                </td>
                <td>
                  <xsl:for-each select="mumie:user_groups/mumie:user_group">
                    <xsl:choose>
                      <xsl:when test="position() = 1">
                        <xsl:call-template name="name"/>
                      </xsl:when>
                      <xsl:otherwise>
                        ,<xsl:call-template name="name"/>
                      </xsl:otherwise>
                    </xsl:choose>
                  </xsl:for-each>
                </td>
                <td>
                  <xsl:call-template name="details"/>
                </td>
              </tr>
            </xsl:for-each>
          </tbody>
        </table>
      </div>
    </body>
  </html>
</xsl:template>

<!--
  Outputs the document type name. This template may be changed in the future to output 
  a name in the user's language.
 -->

<xsl:template name="type">
  <xsl:value-of select="local-name()"/>
</xsl:template>

<!--
  Outputs the category name. This template may be changed in the future to output a
  name in the user's language.
-->

<xsl:template name="category">
  <xsl:value-of select="mumie:category/@name"/>
</xsl:template>

<!--
  Outputs the pure name, as a link to the respective (pseudo-)document.
-->

<xsl:template name="name">
  <xsl:variable name="type" select="local-name()"/>
  <a>
    <xsl:attribute name="href">
      <xsl:call-template name="xsl.pseudodoc-url">
        <xsl:with-param name="context" select="'info'"/>
      </xsl:call-template>
    </xsl:attribute>
    <xsl:value-of select="mumie:name"/>
  </a>
</xsl:template>

<xsl:template name="details">
  <xsl:variable name="type" select="local-name()"/>
  <a class="button-inline">
    <xsl:attribute name="href">
      <xsl:call-template name="xsl.pseudodoc-url">
        <xsl:with-param name="context" select="'info'"/>
      </xsl:call-template>
    </xsl:attribute>
    <xsl:value-of select="$i18n-details"/>
  </a>
</xsl:template>

<xsl:template name="email">
  <xsl:param name="email"/>
  <a>
    <xsl:attribute name="href">
      <xsl:value-of select="concat('mailto:', $email)"/>
    </xsl:attribute>
    <xsl:value-of select="$email"/>
  </a>
</xsl:template>

</xsl:stylesheet>
