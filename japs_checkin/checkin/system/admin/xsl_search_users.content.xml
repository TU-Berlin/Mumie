<?xml version="1.0" encoding="ASCII"?>


<!--
  The MIT License (MIT)
  
  Copyright (c) 2010 Technische Universitaet Berlin
  
  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:
  
  The above copyright notice and this permission notice shall be included in
  all copies or substantial portions of the Software.
  
  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
  THE SOFTWARE.
-->

<!DOCTYPE stylesheet
  [
   <!ENTITY br  "&#xA;">
   <!ENTITY sp  "  ">
  ]
>

<!--
   Author:  Tilman Rassy

   $Id: xsl_search_users.content.xml,v 1.4 2009/03/06 10:57:14 rassy Exp $
-->
<xsl:stylesheet version="1.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:xsl-ext="http://www.mumie.net/xml-namespace/xsl-ext"
                xmlns:mumie="http://www.mumie.net/xml-namespace/document/metainfo"
                xmlns="http://www.w3.org/1999/xhtml"
                exclude-result-prefixes="xsl-ext mumie">

<xsl-ext:import lid="xsl-util" internal="yes"/>
<xsl-ext:import lid="xsl-i18n" internal="yes"/>

<xsl:output method="xml"
            doctype-public="-//W3C//DTD XHTML 1.1 plus MathML 2.0//EN"
            doctype-system="http://www.w3.org/TR/MathML2/dtd/xhtml-math11-f.dtd"
            encoding="ASCII"/>

<xsl-ext:insert-japs-xsl-lib/>

<!-- ================================================================================ -->
<!-- h1: Global parameters and variables                                              -->
<!-- ================================================================================ -->

<!-- Size of the text fields: -->
<xsl:variable name="text-field-size">20</xsl:variable>

<!-- Search request: -->
<xsl:variable name="search-request" select="/mumie:user_search/mumie:request"/>
<xsl:variable name="search-request.id" select="$search-request/mumie:id"/>
<xsl:variable name="search-request.login-name" select="$search-request/mumie:login_name"/>
<xsl:variable name="search-request.first-name" select="$search-request/mumie:first_name"/>
<xsl:variable name="search-request.surname" select="$search-request/mumie:surname"/>

<!-- Search results: -->
<xsl:variable name="search-results" select="/mumie:user_search/mumie:result/mumie:user"/>

<!-- The form-stage: -->
<xsl:variable name="form-stage">
  <xsl:call-template name="xsl.get-param-from-dynamic-data">
    <xsl:with-param name="name">form-stage</xsl:with-param>
    <xsl:with-param name="default">0</xsl:with-param>
  </xsl:call-template>
</xsl:variable>


<!-- ================================================================================ -->
<!-- h1: Utilities                                                                    -->
<!-- ================================================================================ -->

<xsl:template name="text-field">
  <xsl:param name="name"/>
  <xsl:param name="value"/>
  <xsl:param name="size" select="$text-field-size"/>
  <xsl:param name="html-id" select="concat('INPUT.', $name)"/>
  <input id="{$html-id}"
         type="text"
         class="normal"
         name="{$name}"
         value="{$value}"
         size="{$size}"
         onfocus="setInputFocusStyle('{$html-id}')"
         onblur="setInputNormalStyle('{$html-id}')">
  </input>
</xsl:template>

<!-- A tricky recursive template to print the search request items with separating commas, suppressing those
 which are empty. -->

<xsl:template name="print-search-request">
  <xsl:param name="items">id;login-name;first-name;surname;</xsl:param>
  <xsl:param name="last-output"></xsl:param>
  <xsl:variable name="item" select="substring-before($items, ';')"/>
  <xsl:variable name="rest-items" select="substring-after($items, ';')"/>
  <xsl:variable name="output">
    <xsl:choose>
      <xsl:when test="$item='id' and $search-request.id and $search-request.id !=''">
        <xsl:text>id = </xsl:text>
        <xsl:value-of select="$search-request.id"/>
      </xsl:when>
      <xsl:when test="$item='login-name' and $search-request.login-name and $search-request.login-name !=''">
        <xsl:value-of select="$i18n-login-name"/>
        <xsl:text> = "</xsl:text>
        <xsl:value-of select="$search-request.login-name"/>
        <xsl:text>"</xsl:text>
      </xsl:when>
      <xsl:when test="$item='first-name' and $search-request.first-name and $search-request.first-name !=''">
        <xsl:value-of select="$i18n-first-name"/>
        <xsl:text> = "</xsl:text>
        <xsl:value-of select="$search-request.first-name"/>
        <xsl:text>"</xsl:text>
      </xsl:when>
      <xsl:when test="$item='surname' and $search-request.surname and $search-request.surname !=''">
        <xsl:value-of select="$i18n-surname"/>
        <xsl:text> = "</xsl:text>
        <xsl:value-of select="$search-request.surname"/>
        <xsl:text>"</xsl:text>
      </xsl:when>
    </xsl:choose>
  </xsl:variable>
  <xsl:if test="$last-output and $last-output != ''">
    <xsl:text>, </xsl:text>
  </xsl:if>
  <xsl:value-of select="$output"/>
  <xsl:if test="$rest-items and $rest-items !=''">
    <xsl:call-template name="print-search-request">
      <xsl:with-param name="items" select="$rest-items"/>
      <xsl:with-param name="last-output" select="$output"/>
    </xsl:call-template>
  </xsl:if>
</xsl:template>

<!-- ================================================================================ -->
<!-- h1: Button area                                                                  -->
<!-- ================================================================================ -->

<xsl:template name="button-area">
</xsl:template>

<!-- ================================================================================ -->
<!-- h1: Main template                                                                -->
<!-- ================================================================================ -->

<xsl:template match="/">
  <html>
    <head>
      <title>
        <xsl:value-of select="$i18n-title"/>
      </title>
      <link rel="stylesheet" type="text/css">
        <xsl:attribute name="href"><xsl-ext:url lid="css-base"/></xsl:attribute> 
      </link>
      <link rel="stylesheet" type="text/css">
        <xsl:attribute name="href"><xsl-ext:url lid="css-search-users"/></xsl:attribute> 
      </link>
      <script type="text/javascript">
        <xsl:call-template name="input-element-javascript"/>
      </script>
    </head>
    <body>
      <xsl:call-template name="top-bar"/>
      <div class="main-indented">

        <div class="h1-container">
          <div class="breadcrumb">
            <a xsl-ext:href="pge-admin-home"><xsl:value-of select="$i18n-admin"/></a> &gt;
            <a xsl-ext:href="pge-user-management"><xsl:value-of select="$i18n-user-management"/></a> &gt;
          </div>
          <h1><xsl:value-of select="$i18n-title"/></h1>
        </div>

        <div class="search-request">
          <form action="{$xsl.url-prefix}/protected/admin/search-users"
                enctype="multipart/form-data"
                method="post">
            <table class="grid">
              <!-- Id -->
              <tr>
                <td>Id:</td>
                <td>
                  <xsl:call-template name="text-field">
                    <xsl:with-param name="name">id</xsl:with-param>
                    <xsl:with-param name="value" select="$search-request.id"/>
                  </xsl:call-template>
                </td>
              </tr>
              <!-- login name -->
              <tr>
                <td><xsl:value-of select="$i18n-login-name"/>:</td>
                <td>
                  <xsl:call-template name="text-field">
                    <xsl:with-param name="name">login-name</xsl:with-param>
                    <xsl:with-param name="value" select="$search-request.login-name"/>
                  </xsl:call-template>
                </td>
              </tr>
              <!-- first name -->
              <tr>
                <td><xsl:value-of select="$i18n-first-name"/>:</td>
                <td>
                  <xsl:call-template name="text-field">
                    <xsl:with-param name="name">first-name</xsl:with-param>
                    <xsl:with-param name="value" select="$search-request.first-name"/>
                  </xsl:call-template>
                </td>
              </tr>
              <tr>
                <td><xsl:value-of select="$i18n-surname"/>:</td>
                <td>
                  <xsl:call-template name="text-field">
                    <xsl:with-param name="name">surname</xsl:with-param>
                    <xsl:with-param name="value" select="$search-request.surname"/>
                  </xsl:call-template>
                </td>
              </tr>
            </table>
            <input type="hidden" name="form-stage" value="1"/>
            <div class="buttons">
              <button type="submit" name="submit" value="submit">
                <xsl:value-of select="$i18n-submit"/>
              </button>
            </div>
          </form>
        </div>

        <xsl:if test="$form-stage=1">
          <div class="search-result">
            <p>
              <xsl:value-of select="$i18n-search-results-for"/>
              <xsl:text>: </xsl:text>
              <xsl:call-template name="print-search-request"/>
            </p>
            <xsl:choose>
              <xsl:when test="$search-results">
                <table class="genuine" style="margin-left:0em">
                  <thead>
                    <tr>
                      <td>Id</td>
                      <td><xsl:value-of select="$i18n-login-name"/></td>
                      <td><xsl:value-of select="$i18n-name"/></td>
                      <td></td> <!-- Details -->
                      <td></td> <!-- Edit -->
                    </tr>
                  </thead>
                  <tbody>
                    <xsl:for-each select="$search-results">
                      <xsl:sort select="mumie:surname"/>
                      <xsl:sort select="mumie:first_name"/>
                      <tr>
                        <td><xsl:value-of select="@id"/></td>
                        <td><xsl:value-of select="mumie:login_name"/></td>
                        <td><xsl:value-of select="concat(mumie:surname, ', ', mumie:first_name)"/></td>
                        <td>
                          <xsl:variable name="details-url">
                            <xsl:call-template name="xsl.pseudodoc-url">
                              <xsl:with-param name="type-name">user</xsl:with-param>
                              <xsl:with-param name="id" select="@id"/>
                            </xsl:call-template>
                          </xsl:variable>
                          <a class="button-inline" href="{$details-url}">
                            <xsl:value-of select="$i18n-details"/>
                          </a>
                        </td>
                        <td>
                          <a class="button-inline"
                             href="{$xsl.url-prefix}/protected/admin/edit-user?id={@id}">
                            <xsl:value-of select="$i18n-edit"/>
                          </a>
                        </td>
                      </tr>
                    </xsl:for-each>
                  </tbody>
                </table>
              </xsl:when>
              <xsl:otherwise>
                <p>
                  <xsl:value-of select="$i18n-no-users-found"/>
                </p>
              </xsl:otherwise>
            </xsl:choose>
          </div>
        </xsl:if>

      </div>
    </body>
  </html>
</xsl:template>

</xsl:stylesheet>