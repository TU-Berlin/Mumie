<?xml version="1.0" encoding="ASCII"?>


<!--
  The MIT License (MIT)
  
  Copyright (c) 2010 Technische Universitaet Berlin
  
  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:
  
  The above copyright notice and this permission notice shall be included in
  all copies or substantial portions of the Software.
  
  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
  THE SOFTWARE.
-->

<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:xsl-ext="http://www.mumie.net/xml-namespace/xsl-ext"
                xmlns:mumie="http://www.mumie.net/xml-namespace/document/metainfo"
                xmlns="http://www.w3.org/1999/xhtml"
                exclude-result-prefixes="mumie"
                version="1.0">

<xsl-ext:import lid="xsl-util" internal="yes"/>
<xsl-ext:import lid="xsl-document-info-i18n" internal="yes"/>

<xsl:output method="xml"
            doctype-public="-//W3C//DTD XHTML 1.1 plus MathML 2.0//EN"
            doctype-system="http://www.w3.org/TR/MathML2/dtd/xhtml-math11-f.dtd"
            encoding="ASCII"/>

<xsl-ext:insert-japs-xsl-lib/>

<xsl:variable name="doctype" select="local-name(/*)"/>

<xsl:variable name="id" select="/*/@id"/>

<!-- Whether the top bar shall be displayed: -->
<xsl:variable name="with-top-bar">
  <xsl:call-template name="xsl.boolean-to-yes-or-no">
    <xsl:with-param name="boolean">
      <xsl:call-template name="xsl.get-param-from-dynamic-data">
        <xsl:with-param name="name">with-top-bar</xsl:with-param>
        <xsl:with-param name="default">yes</xsl:with-param>
      </xsl:call-template>
    </xsl:with-param>
  </xsl:call-template>
</xsl:variable>

<xsl:template match="/">
  <html>

    <head>
      <title>
        <xsl:value-of select="/*/mumie:name"/>
      </title>
      <link rel="stylesheet" type="text/css">
        <xsl:attribute name="href"><xsl-ext:url lid="css-base"/></xsl:attribute>
      </link>
      <link rel="stylesheet" type="text/css">
        <xsl:attribute name="href"><xsl-ext:url lid="css-document-info"/></xsl:attribute>
      </link>
    </head>

    <body>

      <xsl:if test="$with-top-bar = 'yes'">
        <xsl:call-template name="top-bar"/>
      </xsl:if>

      <div class="main-indented">

        <h1><xsl:value-of select="/*/mumie:pure_name"/></h1>

        <div class="link-area">
          <xsl:variable name="metainfos-url">
            <xsl:call-template name="xsl.document-url">
              <xsl:with-param name="document-type.name" select="$doctype"/>
              <xsl:with-param name="id" select="$id"/>
              <xsl:with-param name="context">info</xsl:with-param>
            </xsl:call-template>
          </xsl:variable>
          <a href="{$metainfos-url}"><xsl:value-of select="$i18n-metainfos"/></a>
          <span class="sep">|</span>
          <xsl:value-of select="$i18n-content"/>
        </div>

        <pre>
          <xsl:apply-templates select="/*/mumie:content/*"/>
        </pre>

        <div class="button-area">
          <a class="button" href="javascript:history.back()" title="{$i18n-back}">
            <xsl:value-of select="$i18n-back"/>
          </a>                                
        </div>  

      </div>
      
    </body>
  </html>

</xsl:template>


<xsl:template name="tag-name">
  <xsl:param name="namespace-prefix"/>
  <xsl:param name="local-name"/>
  <xsl:if test="$namespace-prefix != ''">
    <span class="nmsp-prf"><xsl:value-of select="$namespace-prefix"/></span>
    <span class="tag-colon">:</span>
  </xsl:if>
  <span class="tag-name"><xsl:value-of select="$local-name"/></span>
</xsl:template>

<xsl:template match="*">
  <xsl:variable name="namespace-prefix" select="substring-before(name(), ':')"/>
  <xsl:variable name="local-name" select="local-name()"/>
  <xsl:variable name="children" select="*|node()"/>
  <span class="tag-delim"><xsl:text>&lt;</xsl:text></span>
  <xsl:call-template name="tag-name">
    <xsl:with-param name="namespace-prefix" select="$namespace-prefix"/>
    <xsl:with-param name="local-name" select="$local-name"/>
  </xsl:call-template>
  <xsl:apply-templates select="@*"/>
  <xsl:choose>
    <xsl:when test="$children">
      <span class="tag-delim"><xsl:text>&gt;</xsl:text></span>
      <xsl:apply-templates select="$children"/>
      <span class="tag-delim"><xsl:text>&lt;/</xsl:text></span>
      <xsl:call-template name="tag-name">
        <xsl:with-param name="namespace-prefix" select="$namespace-prefix"/>
        <xsl:with-param name="local-name" select="$local-name"/>
      </xsl:call-template>
      <span class="tag-delim"><xsl:text>&gt;</xsl:text></span>
    </xsl:when>
    <xsl:otherwise>
      <span class="tag-delim"><xsl:text>/&gt;</xsl:text></span>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<xsl:template match="@*">
  <xsl:text> </xsl:text>
  <span class="attr-name"><xsl:value-of select="name()"/></span>
  <span class="attr-eq"><xsl:text>=</xsl:text></span>
  <span class="attr-delim"><xsl:text>"</xsl:text></span>
  <span class="attr-value"><xsl:value-of select="."/></span>
  <span class="attr-delim"><xsl:text>"</xsl:text></span>
</xsl:template>

<xsl:template match="text()">
  <xsl:copy/>
</xsl:template>

<xsl:template match="comment()">
  <span class="comment"><xsl:text>&lt;--</xsl:text>
  <xsl:value-of select="."/>
  <xsl:text>--&gt;</xsl:text></span>
</xsl:template>

</xsl:stylesheet>
